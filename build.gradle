plugins {
	id 'maven-publish'
	alias(libs.plugins.quilt.loom)
}

loom {
	serverOnlyMinecraftJar()
}

archivesBaseName = "${project.variant}_${project.archives_base_name}"
version = project.version
group = project.maven_group

dependencies {
	minecraft libs.minecraft
	mappings loom.layered {
		addLayer quiltMappings.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:v2")
	}
	modImplementation libs.quilt.loader
}

processResources {
	inputs.property(variant, project.variant)

	filesMatching('*.json') {
		expand(
			version: version,
			variant: variant,
			variantOpposite: (variant == "simple") ? "complete" : "simple",
			variantUpper: variant.capitalize()
		)
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 11
}

java {
	sourceCompatibility = JavaVersion.VERSION_11
	targetCompatibility = JavaVersion.VERSION_11

	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${archivesBaseName}" }
	}
}

// Configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}
}
